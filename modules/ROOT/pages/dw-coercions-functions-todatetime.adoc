= toDateTime
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]



[[todatetime1]]
== toDateTime&#40;str: String, formatters: Array<Formatter&#62;&#41;: DateTime

Transforms a `String` value into a `DateTime` value using the first `Formatter` that
matches with the given value to transform.

_Introduced in DataWeave version 2.5.0._

=== Parameters

[%header, cols="1,1,3"]
|===
| Name | Type | Description
| `str` | String | The `String` value to transform into a `DateTime` value.
| `formatters` | Array<Formatter&#62; | The `array` of formatting to use on the `DateTime` value.
|===

=== Example

This example shows how `toDateTime` behaves with different inputs.
It produces output in the `application/dw` format.

==== Source

[source,DataWeave,linenums]
----
%dw 2.0
import * from dw::util::Coercions
import * from dw::Runtime
output application/dw
---
{
  a: toDateTime("2003-10-01 23:57:59Z", [{format: "uuuu/MM/dd HH:mm:ssz"}, {format: "uuuu-MM-dd HH:mm:ssz"}]),
  b: try(() -> toDateTime("2003-10-01 23:57:59Z", [{format: "uuuu/MM/dd HH:mm:ssz"}])).error.message
}
----

==== Output

[source,DataWeave,linenums]
----
{
  a: |2003-10-01T23:57:59Z| as DateTime {format: "uuuu-MM-dd HH:mm:ssz"},
  b: "Could not find a valid formatter for '2003-10-01 23:57:59Z'"
}
----


[[todatetime2]]
== toDateTime&#40;number: Number, unit: MillisOrSecs &#124; Null = null&#41;: DateTime

Transforms a `Number` value into a `DateTime` value
using `milliseconds` or `seconds` as the unit.

_Introduced in DataWeave version 2.4.0._

=== Parameters

[%header, cols="1,3"]
|===
| Name | Description
| `number` | The `Number` value to transform into a `DateTime` value.
| `unit` | The unit to use for the conversion: `"milliseconds"`
         or `"seconds"`. A `null` value for the `unit` field
         defaults to `"seconds"`.
|===

=== Example

This example shows how `toDateTime` behaves with different inputs.
It produces output in the `application/dw` format.

==== Source

[source,DataWeave,linenums]
----
%dw 2.0
import * from dw::util::Coercions
output application/dw
---
{
    fromEpoch: toDateTime(1443743879),
    fromMillis: toDateTime(1443743879000, "milliseconds")
}
----

==== Output

[source,DataWeave,linenums]
----
{
  fromEpoch: |2015-10-01T23:57:59Z|,
  fromMillis: |2015-10-01T23:57:59Z| as DateTime {unit: "milliseconds"}
}
----


[[todatetime3]]
== toDateTime&#40;str: String, format: String &#124; Null = null, locale: String &#124; Null = null&#41;: DateTime

Transforms a `String` value into a `DateTime` value
and accepts a format and locale.

_Introduced in DataWeave version 2.4.0._

=== Parameters

[%header, cols="1,3"]
|===
| Name | Description
| `str` | The `String` value to transform into a `DateTime` value.
| `format` | The formatting to use on the `DateTime` value.
           A `null` value has no effect on the `DateTime` value.
           This parameter accepts Java character patterns based
           on ISO-8601. A `DateTime` value, such as
           `2011-12-03T10:15:30.000000+01:00`, has
           the format `uuuu-MM-dd HH:mm:ssz`.
| `locale` | Optional ISO 3166 country code to use, such as `US`,
           `AR`, or `ES`. A `null` or absent value uses your
           JVM default.
|===

=== Example

This example shows how `toDateTime` behaves with different inputs.
It produces output in the `application/dw` format.

==== Source

[source,DataWeave,linenums]
----
%dw 2.0
import * from dw::util::Coercions
output application/dw
---
{
   a: toDateTime("2015-10-01T23:57:59Z"),
   b: toDateTime("2003-10-01 23:57:59Z","uuuu-MM-dd HH:mm:ssz")
}
----

==== Output

[source,DataWeave,linenums]
----
{
  a: |2015-10-01T23:57:59Z|,
  b: |2003-10-01T23:57:59Z| as DateTime {format: "uuuu-MM-dd HH:mm:ssz"}
}
----

